Module Bookstore{
	Entity Employee{
		Integer IdentificationNumber;
		ShortString LastName {Required;}
		ShortString FirstName {Required;}
		ShortString Code {RegExMatch "\d{7,10}" "Code must have 7 to 10 digits.";}
		DateTime WorkStarted {Required;}
		DateTime WorkFinished;
		Integer TestPeriod {MinValue 1; MaxValue 12;}
		ShortString Iban {Required; Unique; MinLength 21; MaxLength 21;}

		UniqueMultiple 'LastName FirstName';

		ItemFilter FinishBeforeStart "employee => employee.WorkFinished != null && employee.WorkFinished.Value < employee.WorkStarted.Value";
		InvalidData FinishBeforeStart 'It is not allowed to enter a WorkFinished time before the WorkStarted time.';

		Logging{
			AllProperties;
		}
	}

	Entity Manager{
		Extends Bookstore.Employee;

		ShortString Bonus{Required;}
	}

	Entity Education{
		Date Date {Required;}
		LongString Description {Required;}

		Reference Employee;
	}

	Entity Department{
		ShortString Code {AutoCode; DefaultValue 'item => "D+++"'; DenyUserEdit;}
		LongString Description {Required;}

		Logging{
			Log Bookstore.Department.Code;
		}
	}

	Entity EmployeeDepartment{
		Reference Employee {Detail;}
		Reference Department {Required;}

		UniqueMultiple 'Employee Department';
	}
}