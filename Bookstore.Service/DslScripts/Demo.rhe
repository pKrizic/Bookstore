Module Demo
{
    Polymorphic MoneyTransaction
    {
        DateTime EventDate;
        Money Amount;
    }

    Entity TestCodeTable{
        CodeTable;
        MonitoredRecord;
    }

    Entity BorrowMoney
    {
        ShortString FromWhom;
        DateTime EventDate;
        Money Amount;

        Is Demo.MoneyTransaction;
        // The EventDate and Amount are automatically mapped.
    }

    Entity BorrowMoney2
    {
        DateTime EventDate;
        ShortString FromWhom;
        Money Amount;
        Bool Forgotten;

        Is Demo.MoneyTransaction
        {
            Where 'Forgotten = 0'; // SQL snippet, the "Forgotten" column is a "bit".
        }
    }

    Entity LendMoneyAddendum
    {
        Reference LendMoney;
        Money AdditionalAmount;
    }

    Entity LendMoney
    {
        ShortString ToWhom;
        DateTime EventDate;
        Money Amount;

        Is Demo.MoneyTransaction
        {
            Implements Demo.MoneyTransaction.Amount '-Amount';
            // The Amount in the MoneyTransaction related to the LendMoney record will have a negative value.
        }

        Is Demo.MoneyTransaction 'TotalAddendum'
        {
            Implements Demo.MoneyTransaction.Amount '(SELECT -SUM(AdditionalAmount) FROM Demo.LendMoneyAddendum)';
            SqlDependsOn Demo.LendMoneyAddendum;
        }
    }

    Entity TransferMoney
    {
        DateTime EventDate;
        ShortString TransferFrom;
        ShortString TransferTo;
        Money Amount;

        Is Demo.MoneyTransaction; // Implicitly using the 'Amount' value.

        Is Demo.MoneyTransaction 'Subtract'
        {
            Implements Demo.MoneyTransaction.Amount '-Amount';
        }
    }

    Entity TransactionComment
    {
        Reference MoneyTransaction;
        LongString Comment;
    }

    Entity LendMoney2
    {
        ShortString ToWhom;
        // When using SqlImplementation, the properties are not automatically inherited from the polymorphic.
        DateTime EventDate;
        Money Amount;

        Is Demo.MoneyTransaction
        {
            SqlImplementation "SELECT lm.ID, lm.EventDate, Amount = -lm.Amount FROM Demo.LendMoney2 lm"
            {
                AutoDetectSqlDependencies;
            }
        }
    }
}